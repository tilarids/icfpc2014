; hunter bot - try to run towards lambdaman

        int 3       ; our index
        int 6
        mov g,b     ; our current dir
        int 1       ; get lambdaman current coords in (a,b)
        mov c,a
        mov d,b
        int 3
        int 5       ; get our curr. coords in (a,b)
        sub c,a
        sub d,b     ; (c,d) - relative coords of enemy

        jlt m0,d,0
        ; y > 0 - upper half
        jlt m1,c,0
        ; x > 0 - NE quadrant
        jlt m2,c,d
        ; x > y => dir 1
        mov c,1
        jeq done,a,a
m2:     ; x < y => dir 0
        mov c,0
        jeq done,a,a

m1:     ; x < 0 - NW quadrant
        mov a,0
        sub a,c     ; a = -x
        jlt m3,a,d
        ; -x > y => dir 3
        mov c,3
        jeq done,a,a
m3:     ; -x < y => dir 0
        mov c,0
        jeq done,a,a

m0:     ; y < 0 - lower half
        mov b,0
        sub b,d     ; b = -y
        jlt m5,c,0
        ; x > 0 - SE quadrant
        jlt m4,c,b
        ; x > -y => dir 1
        mov c,1
        jeq done,a,a
m4:     ; x < -y => dir 2
        mov c,2
        jeq done,a,a

m5:     ; x < 0 - SW quadrant
        mov a,0
        sub a,c     ; a = -x
        jlt m6,a,b
        ; -x > -y => dir 3
        mov c,3
        jeq done,a,a
m6:     ; -x < -y => dir 2
        mov c,0

done:
        int 3
        int 6       ; a = vitality
        jeq panic,a,1
        ; normal or invisible mode
        xor c,2     ; inverse direction
panic:
        xor c,2     ; inverse direction
        mov a,c
exit:
        int 0
        hlt
